@name Example Door
@persist Door:entity
if(first()){
    #[
        What prop Do You want to spawn?
        Copy the model and paste it below.
    ]#
    Prop="models/hunter/blocks/cube2x2x05.mdl"
    Pos=entity():toWorld(vec(0,0,50))
    Ang=entity():toWorld(ang(90,90,0))
    Door=propSpawn(Prop,Pos,Ang,1)
    Door:setAlpha(0)
    Door:propDraw(0)
    entity():parentTo(Door)
    
    #[
        Create a hologram that you would 
        like to be attached to the door.
    ]#
    holoCreate(1,entity():toWorld(vec(0,0,50)),vec(1),entity():toWorld(ang(0,-90,0)),vec(255),"models/freeman/vault/vault_door.mdl")
    holoParent(1,Door)
}
Material=Door:getMaterial()
if(changed(Material)){
    switch(Material){
        case "Models/effects/vol_light001",
        case "sprites/heatwave",
            #[
                When the door is open,
                run this segment of code.
            ]#
            holoEntity(1):animate(4,2)
            Door:soundPlay(1,3,"doors\\gate_move1.wav")
            break
        default,
            #[
                When the door is closed,
                run this segment of code.
            ]#
            holoEntity(1):animate(1,1)
            Door:soundPlay(1,1.5,"doors\\door_metal_rusty_move1.wav")
            timer("Delayed Reaction",1500)
            break
    }
}
if(clk("Delayed Reaction")){
    #[
        This segment will run shortly after the 
        door has been opened. 
        This is useful if you want to make like 
        a door shutting sound or something.
    ]#
    Door:soundPlay(1,2,"doors\\door_metal_medium_open1.wav")
}
interval(750)
